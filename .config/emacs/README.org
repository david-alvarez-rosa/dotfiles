#+title: GNU Emacs Configuration
#+language: en
#+author: David Álvarez Rosa
#+email: david@alvarezrosa.com
#+description: My personal GNU Emacs configuration file.
#+property: header-args :tangle init.el


GNU Emacs is a free (as in /freedom/) +operating system+ text editor, created
by GNU Project founder Richard Matthew Stallman. I've been using this
magnificent piece of software daily for years now and I can assure you that
is--without a doubt--the best multi-purpose program ever written,
over-performing any other so-called text editors, such as Vi(m)--the editor of
the beast-- or, even worse, Microsoft VS Code malware, among others. I consider
myself a member of the Church of Emacs, my only religion, and I'm a firmly
believer in St. I-GNU-cius--who is a saint, but not a virgin--and to whom I
pray daily. In my day-to-day life I try to stick to the FOSS philosophy, while
living a life of moral chastity and purity, pursuing sainthood mainly through
extensively using org-mode.

There are some people out there that don't really get the meaning and purpose
of Emacs. They think that it is just another bloated text editor that doesn't
adhere to the KISS principle or the Unix philosophy. It is not. First, it is
not a text editor, it is a Lisp interpreter (actually, Elisp), a
Turing-complete programming language (therefore, offering limitless
possibilities). Secondly, there is no contradiction between using Emacs and
KISS or Unix philosophy: Emacs--at least for me--is a container for
"do-one-thing-and-do-it-well" packages that behave similarly, I find no
conflict here. Lastly, Emacs is a gift from God.


* Table of Contents                                                     :TOC:
- [[#preamble][Preamble]]
  - [[#installation][Installation]]
  - [[#about-this-configuration][About this Configuration]]
  - [[#license-and-copyright][License and Copyright]]
- [[#loading-and-package-management][Loading and Package Management]]
- [[#general-configuration][General Configuration]]
  - [[#personal-information][Personal Information]]
  - [[#server-mode][Server Mode]]
  - [[#custom-file][Custom File]]
  - [[#authentication-file][Authentication File]]
  - [[#mac-os][Mac OS]]
  - [[#change-yesno-questions-to-yn][Change yes/no Questions to y/n]]
  - [[#file-management][File Management]]
  - [[#buffer-management][Buffer Management]]
  - [[#window-management][Window Management]]
  - [[#auto-fill-mode][Auto Fill Mode]]
  - [[#indentation-enhancements][Indentation Enhancements]]
  - [[#subword-mode][Subword Mode]]
  - [[#region-selection][Region Selection]]
  - [[#scrolling-and-point-location][Scrolling and Point Location]]
  - [[#character-pairs][Character Pairs]]
  - [[#html-renderer][HTML Renderer]]
  - [[#completion-inteface][Completion Inteface]]
  - [[#spell-checking-and-correcting][Spell Checking and Correcting]]
  - [[#sudo-edit][Sudo Edit]]
  - [[#chronometer][Chronometer]]
- [[#user-interface][User Interface]]
  - [[#theme][Theme]]
  - [[#modeline][Modeline]]
  - [[#font][Font]]
  - [[#basic-interface-settings][Basic Interface Settings]]
  - [[#transparency-alpha][Transparency Alpha]]
  - [[#distraction-free-mode][Distraction Free Mode]]
- [[#programming-configuration][Programming Configuration]]
  - [[#general-configuration-1][General Configuration]]
  - [[#amazon-specific][Amazon Specific]]
  - [[#cc][C/C++]]
  - [[#typescript][TypeScript]]
  - [[#python][Python]]
  - [[#java][Java]]
  - [[#web-development][Web Development]]
  - [[#octavematlab][Octave/Matlab]]
  - [[#r][R]]
- [[#org-mode][Org Mode]]
  - [[#speed-commands][Speed Commands]]
  - [[#refile][Refile]]
  - [[#capture-templates][Capture Templates]]
  - [[#state-keywords][State Keywords]]
  - [[#code-evaluation][Code Evaluation]]
  - [[#interface][Interface]]
  - [[#agenda][Agenda]]
  - [[#archive][Archive]]
  - [[#table-of-contents][Table of Contents]]
- [[#markup-languages][Markup Languages]]
  - [[#markdown-mode][Markdown Mode]]
  - [[#texlatex][TeX/LaTeX]]
  - [[#ledger][Ledger]]
- [[#pdf-viewer][PDF Viewer]]
- [[#email-management][Email Management]]
  - [[#contexts-for-multiple-accounts][Contexts for Multiple Accounts]]
  - [[#bookmarks][Bookmarks]]
  - [[#update-and-notification][Update and Notification]]
  - [[#queued-email][Queued Email]]
  - [[#emacs-unified-directory-client][Emacs Unified Directory Client]]
  - [[#compositionsending][Composition/Sending]]
  - [[#attachment-management][Attachment Management]]
  - [[#meetings][Meetings]]
- [[#web-feed-reader][Web Feed Reader]]
  - [[#elfeed-org][Elfeed Org]]
  - [[#elfeed-goodies][Elfeed Goodies]]
  - [[#custom-functions][Custom Functions]]
- [[#internet-relay-chat-irc][Internet Relay Chat (IRC)]]
  - [[#nickname-highlighter][Nickname Highlighter]]
- [[#useful-resources][Useful Resources]]

* Preamble
** Installation
This configuration is written in a literate style using Org mode (an Emacs
major mode). I do not recommend using this configuration "as is", but take it
as an inspiration for your own personal config. However, if you want to use it
that way, it's posible. You just need to start with a bare Emacs installation
(i.e., without any =.emacs= file in your home directory or =.emacs.d=
directory). With this done, just:
- Download this =init.org= file
- Create =.config/emacs/= directory if it does not exists.
- Open it with Emacs and press the function =C-c C-v C-t= for tangling all the
  source code blocks, i.e, call the function =org-babel-tangle= that will
  extract all source blocks from this file and write them into a file called
  =init.el= (inside the previously created directory).
- Open Emacs and enjoy!

  *NOTE:* First time you launch Emacs all needed packages will be
  /automagically/ installed for you, so you will need to wait for a couple of
  minutes. After that, Emacs will launch much faster.

** About this Configuration
Underlying principles:
- Built-in alternatives have been preferred to standalone (M)ELPA packages.
- User define commands are usually bound with =C-c= prefix, to avoid clashing
  with Emacs defaults (that usually use =C-x=).
- Minimalist, yet powerful.

Some utilities:
- Email Management, with mu4e and bound to =C-c e= ("e" for email).
- Web Feed Reader, with Elfeed. Bound to =C-c f= ("f" for feed).
- Org mode Agenda, bound to =C-c a= ("a" for agenda).
- Music Management, bound to =C-c m= ("m" for music).
- IRC client, with ERC. Bound to =C-c i= ("i" for IRC).
- Trasparency alpha toggler. Bound to =C-c t= ("t" for transparency).
- Light/Dark theme. Bound to =C-c d= ("d" for dark). This cycles themes.
- Git, with Magit, bound to =C-c g= ("g" for git).
- Kill all buffers except current and *scratch*. Bound to =C-c k= ("k" for
  killing). This is kind of "restaring" Emacs session without actually closing
  it.

Conventions:
- Functions and variables defined by me are prefixed with my name and a slash,
  i.e., =dalvrosa/= to namespace them and avoid conflicts.

** License and Copyright
Copyright 2017-2022 David Álvarez Rosa.

Files are licensed under the same license as Emacs (GPL) unless otherwise
specified. See the LICENSE file for more information.

Any external/third party works included in this work are licensed under their
own licenses--refer to the submodules or packages for more information.

* Loading and Package Management
Make startup faster by reducing the frequency of garbage collection and then
use a hook to lower threshold back after Emacs startup. The loading time can be
check with the function =emacs-init-time=.
#+begin_src elisp
  (setq gc-cons-threshold most-positive-fixnum)

  ;; Lower threshold back to 8 MiB (default is 800kB)
  (add-hook 'emacs-startup-hook
            (lambda () (setq gc-cons-threshold (expt 2 23))))
#+end_src

Add MELPA package repositories.
#+begin_src elisp
(require 'package)
(add-to-list 'package-archives '("melpa" . "https://melpa.org/packages/"))
#+end_src

=use-package= will be used for general package management. Ensure it is
installed and load the package.
#+begin_src elisp
  (unless (package-installed-p 'use-package)
    (package-refresh-contents)
    (package-install 'use-package))

  (eval-when-compile
    (require 'use-package))
#+end_src

Always ensure that all package are installed when starting Emacs. If a
particular package does not exist, it will be installed automagically.
#+begin_src elisp
  (setq use-package-always-ensure t)
  (setq use-package-always-defer t)
#+end_src

* General Configuration
This section contains all configuration related to main settings for built-in
Emacs features.

** Personal Information
Basic personal information (name and mail address). Email variable will be
changed accordingly when using =mu4e= contexts (see configuration below).
#+begin_src elisp
  (setq user-full-name "David Álvarez Rosa")
  (setq user-mail-address "david@alvarezrosa.com")
#+end_src

Load file containing =dalvrosa/at-work= bool.
#+begin_src elisp
  (load-file "~/.config/emacs/at-work.el")
#+end_src

** Server Mode
Start the Emacs server mode from this instance, so that all =emacsclient= calls
are routed here. This way, files can be sent to the current Emacs instance,
instead of opening a new one (much faster).
#+begin_src elisp
  (server-start)
#+end_src

** Custom File
Place all custom-defined variables in their own file and store the custom
variable file in the local machine’s home directory outside of version
control. This enables us to keep specific config local to each machine.
#+begin_src elisp
  (setq custom-file "~/.config/emacs/custom.el")
  (load custom-file t)
#+end_src

** Authentication File
The auth-source library is simply a way for Emacs and Gnus, among others, to
answer the old burning question “What are my user name and password?”

This is used for email and IRC log in, among others. The authentication file
looks as follows.
#+begin_src conf :tangle no
  machine mymachine login myloginname password mypassword port myport
  #+end_src

Customize location of authentication file (following XDB Base Directory
specification).
#+begin_src elisp
  (setq auth-sources '("~/.local/share/authinfo.gpg"))
#+end_src

** Mac OS
I currently use Mac on my work laptop, and there are some specific things to
OS X.

Ensure environment variable inside Emacs look the same as shell.
#+begin_src elisp
  (when (eq system-type 'darwin)
    (use-package exec-path-from-shell
      :demand t
      :config
      (exec-path-from-shell-initialize)))
#+end_src

Append to path for using mu4e.
#+begin_src elisp
  (when (eq system-type 'darwin)
    (add-to-list 'load-path "/opt/homebrew/share/emacs/site-lisp/mu/mu4e"))
#+end_src

** Change yes/no Questions to y/n
Shorter and faster.
#+begin_src elisp
  (defalias 'yes-or-no-p 'y-or-n-p)
#+end_src

** File Management
Configuration related to file management.

*** Dired Mode
Dired is the main mode for Emacs file-manager operations. The name “Dired”
stands for “directory editor”.

Dired buffer can be modified by making them editable presing =C-x C-q=. With
this is posible to bulk-rename files, or to change file ownerships and
privileges, just thinking about the file listing as an (Emacs) editable text
file.

Show file sizes in a human readable style.
#+begin_src elisp
  (setq-default dired-listing-switches "-alh --group-directories-first --color")
  (when (eq system-type 'darwin)
    (setq insert-directory-program "/opt/homebrew/bin/gls"))
#+end_src

Ability to use =a= to visit a new directory or file in Dired instead of using
=RET=. =RET= works just fine, but it will create a new buffer for every
interaction whereas =a= reuses the current buffer.
#+begin_src elisp
  (put 'dired-find-alternate-file 'disabled nil)
#+end_src

**** Dired Narrow
This package provides live filtering of files in dired buffers. In general,
after calling the respective narrowing function you type a filter string into
the minibuffer. With =/= start fuzzy matching, then use the dired buffer as
usual, and =g= for going back to the complete file listing.
#+begin_src elisp
  (use-package dired-narrow
    :after dired
    :bind (:map dired-mode-map
                ("/" . 'dired-narrow-fuzzy)))
#+end_src

*** File Backups, Autosaves and Interlock
Keep backups, autosaves and interlocks in the "temp" directory. Take into
account that this is a dangerous option with respect the disappearance of
files, but I always use version control for important projects, so that is not
a problem for me.
#+begin_src elisp
  (setq backup-directory-alist
        `((".*" . ,temporary-file-directory)))
  (setq auto-save-file-name-transforms
        `((".*" ,temporary-file-directory t)))
  (setq lock-file-name-transforms
        `((".*" ,temporary-file-directory t)))
#+end_src

** Buffer Management
Buffers, in Emacs terminology, hold the contents that display/edit in
windows. This are the configurations I use.

*** Ibuffer Mode
Ibuffer is an advanced replacement for BufferMenu, which lets you operate on
buffers much in the same manner as Dired. The most important Ibuffer features
are highlighting and various alternate layouts. Ibuffer is part of Emacs since
version 22.
#+begin_src elisp
  (global-set-key (kbd "C-x C-b") 'ibuffer)
#+end_src

Start in expert mode (without asking for confirmation).
#+begin_src elisp
  (setq ibuffer-expert t)
#+end_src

Kill current buffer without asking for which buffer to kill.
#+begin_src elisp
  (global-set-key (kbd "C-x k") 'kill-current-buffer)
#+end_src

*** Kill All Function
Custom function for killing all buffers except current and *scratch*. Useful
for "restarting" Emacs without closing it.
#+begin_src elisp
  (defun dalvrosa/kill-all-other-buffers ()
    "Kill all buffers except current and *scratch*."
    (interactive)
    (delete-other-windows)
    (setq scratch (get-buffer "*scratch*"))
    (mapc 'kill-buffer (delq scratch (delq (current-buffer) (buffer-list)))))
  (global-set-key (kbd "C-c k") 'dalvrosa/kill-all-other-buffers)
#+end_src

*** Narrowing
Narrowing buffers is very useful, however this commands are disabled by default
because new users often find them confusing. However, this configuration is
*not* for newbies, so enable them.
#+begin_src elisp
  (put 'narrow-to-region 'disabled nil)
  (put 'narrow-to-page 'disabled nil)
  (put 'LaTeX-narrow-to-environment 'disabled nil)
#+end_src

I use =C-x n n= for narrowing to region and then =C-x n w= for widen (i.e.,
reverting to full page), but there are more narrow functions (enviroment
dependent), e.g., =C-x n d= for narrowing to defun (useful for narrowing
functions).

** Window Management
Split and follow for creating new windows.
#+begin_src elisp
  (defun dalvrosa/split-and-follow-horizontally ()
    (interactive)
    (split-window-below)
    (balance-windows)
    (other-window 1))
  (global-set-key (kbd "C-x 2") 'dalvrosa/split-and-follow-horizontally)

  (defun dalvrosa/split-and-follow-vertically ()
    (interactive)
    (split-window-right)
    (balance-windows)
    (other-window 1))
  (global-set-key (kbd "C-x 3") 'dalvrosa/split-and-follow-vertically)
#+end_src

New bind for killing buffer *and* window at the same time. To do this press
=C-x C-k=, by default is bind to =C-x 4 0=, that is far more complex.
#+begin_src elisp
  (global-set-key (kbd "C-x C-k") 'kill-buffer-and-window)
#+end_src

*** Wind Move
Is built into Emacs (since version 21). It lets you move point from window to
window using a direction. This is easier to type than =C-x o= and, for some
users, may be more intuitive. I have configured the movement between windos
with Super and standard Emacs keys.
#+begin_src elisp
  (global-set-key (kbd "s-f") 'windmove-right)
  (global-set-key (kbd "s-b") 'windmove-left)
  (global-set-key (kbd "s-n") 'windmove-down)
  (global-set-key (kbd "s-p") 'windmove-up)
#+end_src

Using shift modifier on top will swap windows instead.
#+begin_src elisp
  (global-set-key (kbd "s-F") 'windmove-swap-states-right)
  (global-set-key (kbd "s-B") 'windmove-swap-states-left)
  (global-set-key (kbd "s-N") 'windmove-swap-states-down)
  (global-set-key (kbd "s-P") 'windmove-swap-states-up)
#+end_src

*** Winner Mode
Global minor mode that allows to “undo” and “redo” changes in window
configuration. It is included in GNU Emacs.
#+begin_src elisp
  (winner-mode 1)
#+end_src

*** Ace Window
Package for selecting a window to switch to. What it does is: when there are
two windows, Ace Window will call =other-window=. If there are more, each
window will have the first character of its window label highlighted at the
upper left of the window. Pressing that character will either switch to that
window or filter to the next character needed to select a specific window.
#+begin_src elisp
  (use-package ace-window
    :bind ("M-o" . 'ace-window)
    :config (setq aw-scope 'frame))
#+end_src

Some useful features, is the posibility to swap windows, by calling
=ace-window= with a prefix argument, i.e., =C-u=. And is also posbile to delete
the selected window by prefixing double, i.e., with =C-u C-u=.

You can also start by calling ace-window and then decide to switch the action
to =delete= or =swap= etc. There are a bunch of command here (check them with
=?= when =ace-window= is activated), but the ones I use the most are:
- Delete window with =x=.
- Swap windows with =m= ("m" from move).

** Auto Fill Mode
Long lines are a bad practice. Please, don't use them, I find them quite
annoying. Stick to at most 80 characters. Use =M-q= for filling paragraphs when
editing (i.e., always ensure the =fill-column= limit).
#+begin_src elisp
  (add-hook 'text-mode-hook 'turn-on-auto-fill)
  (setq-default fill-column 79)
  (add-hook 'text-mode-hook
            (lambda ()
              (set-fill-column 72)))
#+end_src

Custom unfill function with =M-Q=.
#+begin_src elisp
  (defun dalvrosa/unfill-paragraph (&optional region)
    "Takes a multi-line paragraph and makes it into a single line of text."
    (interactive (progn (barf-if-buffer-read-only) '(t)))
    (let ((fill-column (point-max))
          ;; This would override `fill-column' if it's an integer.
          (emacs-lisp-docstring-fill-column t))
      (fill-paragraph nil region)))
  (define-key global-map (kbd "M-Q") 'dalvrosa/unfill-paragraph)
#+end_src

Custom function for killing region after unfill with =M-W=.
#+begin_src elisp
  (defun dalvrosa/unfill-paragraph-and-kill (beg end)
    "Save the current region to the kill ring after unfilling it."
    (setq dalvrosa/previous-major-mode major-mode)
    (interactive "r")
    (copy-region-as-kill beg end)
    (with-temp-buffer
      (funcall dalvrosa/previous-major-mode)
      (yank)
      (dalvrosa/unfill-paragraph (mark-whole-buffer))
      (mark-whole-buffer)
      (kill-region (point-min) (point-max))))
  (define-key global-map (kbd "M-W") 'dalvrosa/unfill-paragraph-and-kill)
#+end_src

** Indentation Enhancements
Allow easier indentation with better keys after calling =indent-rigidly=
function with =C-x TAB=.
#+begin_src elisp
  (define-key indent-rigidly-map "<" 'indent-rigidly-left)
  (define-key indent-rigidly-map "b" 'indent-rigidly-left)
  (define-key indent-rigidly-map ">" 'indent-rigidly-right)
  (define-key indent-rigidly-map "f" 'indent-rigidly-right)
  (define-key indent-rigidly-map "B" 'indent-rigidly-left-to-tab-stop)
  (define-key indent-rigidly-map "F" 'indent-rigidly-right-to-tab-stop)
#+end_src

** Subword Mode
Emacs treats camelCase strings as a single word by default, change this
behaviour.
#+begin_src elisp
  (global-subword-mode 1)
#+end_src

** Region Selection
In Emacs terminology the region is the selected portion of the text.

Real Emacs knights don't use shift to mark things, so disable it.
#+begin_src elisp
  (setq shift-select-mode nil)
#+end_src

Set delete selection mode, so typed text replaces the selection if the
selection is active. Otherwise, typed text is just inserted at point regardless
of any selection.
#+begin_src elisp
  (delete-selection-mode 1)
#+end_src

*** Expand Region
Expand region increases the selected region by semantic units. Just keep
pressing the key until it selects what you want. Expand region is done by
pressing ~C-=~ and contracting by prefixing the shortcut with a negative
argument argument, i.e., ~C-- C-=~.
#+begin_src elisp
  (use-package expand-region
    :bind ("C-=" . 'er/expand-region))
#+end_src

*** Upcase/Downcase Region
Command for upcasing =C-x C-u= or downcasing =C-x C-l= current region are also
disabled by default, enable them.
#+begin_src elisp
  (put 'upcase-region 'disabled nil)
  (put 'downcase-region 'disabled nil)
#+end_src

** Scrolling and Point Location
I personally don't like automatic scrolling (centering point vertically in the
window when point moves out of the visible portion of the text) so I have it
disabled (just set the following number to a large number, greater than 100).
#+begin_src elisp
  (setq scroll-conservatively 101)
#+end_src

Set keys for forward/backward between paragraphs (this is similar to =C-f= vs
=M-f=) .
#+begin_src elisp
  (define-key global-map (kbd "M-n") 'forward-paragraph)
  (define-key global-map (kbd "M-p") 'backward-paragraph)
#+end_src

Scroll window pane with keyboard, but without changing cursor line.
#+begin_src elisp
  (global-set-key (kbd "C-M-n") 'scroll-up-line)
  (global-set-key (kbd "C-M-p") 'scroll-down-line)
#+end_src

Controls if scroll commands move point to keep its screen position unchanged. A
value of t means point keeps its screen position if the scroll command moved it
vertically out of the window, e.g. when scrolling by full screens.
#+begin_src elisp
  (setq scroll-preserve-screen-position t)
#+end_src

With =C-x C-n= Use the current column of point as the semipermanent goal column
for =C-n= and =C-p= in the current buffer. When a semipermanent goal column is
in effect, those commands always try to move to this column, or as close as
possible to it, after moving vertically. The goal column remains in effect
until canceled (with =C-u C-x C-n=). This command is disabled by default, I
enable it.
#+begin_src elisp
  (put 'set-goal-column 'disabled nil)
#+end_src

Right and left scrolling commands are trigered with =C-c >= and =C-c <=
respectively. Scroll left is disabled by default, I enable it.
#+begin_src elisp
  (put 'scroll-left 'disabled nil)
#+end_src

Two very useful commands for recentering window are =C-l= and =C-M-l=, the
former is a well-known Emacs command, and the latter is for =reposition-window=
function, that makes the current definition and/or comment visible (i.e., it
attempts to scroll the window so the maximum content is visible).

*** Avy
Package for jumping to visible text using a char-based decision tree. I use
both =C-:= for jumping to the currently visible CHAR1 followed by CHAR2 (i.e.,
jump to a sequence of 2 chars in any window/frame) and =M-g w= for jumping to
the currently visible CHAR at a word start.
#+begin_src elisp
  (use-package avy
    :bind (("C-:" . 'avy-goto-char-2)
           ("M-g w" . 'avy-goto-word-1)))
#+end_src

** Character Pairs
It can be useful to insert parentheses, braces, quotes and the like in matching
pairs, e.g., pressing “(” inserts "()", with the cursor in between.
#+begin_src elisp
  (electric-pair-mode t)
#+end_src

Highlight matching braces.
#+begin_src elisp
  (show-paren-mode 1)
#+end_src

** HTML Renderer
Configure shr HTML render engine for converting HTML to text. I like the HTML
to be converted to text, without fancy fonts, and filled to 72 characters.
#+begin_src elisp
  (setq shr-use-fonts nil)
  (setq shr-width 72)
#+end_src

** Completion Inteface
After years using Helm I have decided to switch to Ivy, Counsel and Swiper as
completion framework.

Ivy is the generic completion mechanism for Emacs and aims to be more
efficient, smaller, simpler, and smoother to use compared to other completion
schemes yet highly customizable.
#+begin_src elisp
  (use-package ivy
    :init (ivy-mode)
    :config
    (setq ivy-use-virtual-buffers t)
    (setq enable-recursive-minibuffers t)
    (setq ivy-count-format "%d/%d ")
    (setq ivy-initial-inputs-alist nil)
    :bind ("C-c C-r" . 'ivy-resume))
#+end_src

I have enable Ivy virtual buffers so that Ivy will add additional virtual
buffers to the buffers list, such as recent files and bookmarks (those are
highlighted with ivy-virtual face).

Another nice feature is using =C-c C-r= for resuming previous
Ivy/Counsel/Swiper instance.

This package comes with rich transformers for commands from ivy and counsel,
makes the completion interface more friendly (showing more information).
#+begin_src elisp
  (use-package ivy-rich
    :init (ivy-rich-mode 1))
#+end_src

Counsel is a collection of Ivy-enhanced versions of common Emacs commands, i.e,
takes Ivy completion framework even further providing versions of common Emacs
commands that are customised to make the best use of Ivy.
#+begin_src elisp
  (use-package counsel
    :init (counsel-mode)
    :bind ("C-c r" . 'counsel-register))
#+end_src

I have added =C-c r= for interactively choosing a register. Register are very
useful for saving window/frame configuration =C-x r w=/=C-x r f=, current point
position =C-x r SPC= and saving text =C-x r s=.

Swiper is an alternative to isearch that uses Ivy to show an overview of all
matches.
#+begin_src elisp
  (use-package swiper
    :bind ("C-s" . 'swiper-isearch))
#+end_src

Some useful commands for the Ivy/Counsel/Swiper framework that I use are =M-i=
for inserting current candidate to minibuffer and =C-r= for recursive
completion session through the command's history (works like in a Bash
session). Also =M-o= for showing all the possible options/actions and =S-SPC=
for deleting the current input, and reseting the candidates list to the
currently restricted matches (narrowing search). To open the search results in
a separate buffer, press =C-c C-o= for occur.

*** Keybinding Panel
Nice utility (=which-key=) for displaying available keybindings in a popup
panel. You get an overview of what keybindings are available based on the
prefix keys you entered.
#+begin_src elisp
  (use-package which-key
    :init (which-key-mode))
#+end_src

** Spell Checking and Correcting
Commands to check the spelling of a single word or of a portion of a buffer.
These commands only work if a spelling checker program, one of Hunspell,
Aspell, Ispell or Enchant, is installed. These programs are not part of Emacs,
but one of them is usually installed on GNU/Linux and other free operating
systems. I have installed Aspell with =pacman -S aspell aspell-es aspell-en=
(for isntalling english and spanish dictionaries).

I set the default dictionary to english, since I write more in english than in
spanish these days. This can be changed with =ispell-change-dictionary=
function.
#+begin_src elisp
  (setq ispell-dictionary "english")
#+end_src

Main command for spell checking is =M-$= for check and correct pelling of the
word at point, but if the region is active, do it for all words in the region
instead. When this command encounters what appears to be an incorrect word, it
asks you what to do, here are some valid responses (see them all with =?=):
- =digit= for replacing the word.
- =SPC= for skipping this word, and continue.
- =r= replace the word with some user-defined string.
- =a= accept the word only in this editing session (there is =A= variant, for
  only in this buffer).
- =i= insert word to personal dictionary (there is =u= variant, for inserting
  the lowercase version of that word in the dictionary).

It is also useful the use spelling *on the fly* with =flyspell-mode=, that
highlights all misspelled words (the variant =flyspell-prog-mode= is for
programming language, where spelling should occur only for comments and
strings). I have enabled it by default.
#+begin_src elisp
  (add-hook 'text-mode-hook 'flyspell-mode)
  (add-hook 'prog-mode-hook 'flyspell-prog-mode)
#+end_src

** Sudo Edit
Utility for opening files with =sudo= (call the =sudo-edit= function).
#+begin_src elisp
  (use-package sudo-edit)
#+end_src

** Chronometer
#+begin_src elisp
  (use-package chronometer)
#+end_src

* User Interface
Settings related to the user interface, such as the theme, the modeline, the
font, other basic interface settings, and the transparency alpha.

** Theme
I am using Doom Themes. I find creating own theme is too much work and it's now
worth your time. In the past I used Spacemacs theme and I was nice, but I
became bored of it. Doom Themes provides nice themes such as =doom-one= (dark),
=doom-one-ligth= (light), and the classic =zenburn= of course.
#+begin_src elisp
  (use-package doom-themes
    :demand t
    :config
    (setq doom-themes-enable-bold t
          doom-themes-enable-italic t)
    (load-theme 'doom-solarized-light t)
    ;; Corrects (and improves) org-mode's native fontification.
    (doom-themes-org-config))
#+end_src

Some themes I also like are =doom-one= (dark), =doom-peacock= (dark) and
=doom-tomorrow-day= (light).

Custom keybindings for cycling between themes in Emacs with =C-c d=. Binding
cames from toggling dark theme (d for "dark").
#+begin_src elisp
  (setq dalvrosa/themes '(doom-solarized-light doom-zenburn))
  (setq dalvrosa/themes-index 0)

  (defun dalvrosa/cycle-theme ()
    "Cycle through themes defined in dalvrosa/themes variable."
    (interactive)
    ;; Disable current themes.
    (mapc #'disable-theme custom-enabled-themes)
    ;; Load new theme.
    (setq dalvrosa/themes-index (% (1+ dalvrosa/themes-index) (length dalvrosa/themes)))
    (setq dalvrosa/theme (nth dalvrosa/themes-index dalvrosa/themes))
    (load-theme dalvrosa/theme t)
    ;; Resets powerline.
    (when (fboundp 'powerline-reset)
      (powerline-reset)))
  (global-set-key (kbd "C-c d") 'dalvrosa/cycle-theme)
#+end_src

Consider all custom themes as safe (don't ask for confirmation when loading
it).
#+begin_src elisp
  (setq custom-safe-themes t)
#+end_src

** Modeline
For the modeline I am using also Doom Modeline. This package requires the fonts
included with =all-the-icons= to be installed with =M-x
all-the-icons-install-fonts=. I have configured it to show column number
instead of percentage buffer position.
#+begin_src elisp
  (use-package doom-modeline
    :demand t
    :init (doom-modeline-mode 1)
    :config (setq column-number-mode t)
    (setq doom-modeline-buffer-file-name-style 'relative-from-project)
    (setq doom-modeline-percent-position nil))
#+end_src

Disable load average.
#+begin_src elisp
  (setq display-time-default-load-average nil)
#+end_src

Enable nyan cat.
#+begin_src elisp
  (use-package nyan-mode
    :after doom-modeline
    :init (nyan-mode)
    :config
    (nyan-start-animation)
    (nyan-toggle-wavy-trail))
#+end_src

** Font
Set default font. I'm currently using Hack (in the past I used Inconsolata).
#+begin_src elisp
  (set-face-attribute 'default nil :font "Hack" :height 120)
#+end_src

Add ability to scale font for frame, instead that for an specifically
buffer. I've needed to wrap all the functions to reset =doom-modeline= cache
afterwards.
#+begin_src elisp
  (use-package default-text-scale
    :init (default-text-scale-mode)
    :config (setq default-text-scale-amount 20)
    (advice-add 'default-text-scale-reset :after 'doom-modeline-refresh-font-width-cache)
    (advice-add 'default-text-scale-decrease :after 'doom-modeline-refresh-font-width-cache)
    (advice-add 'default-text-scale-increase :after 'doom-modeline-refresh-font-width-cache)
    :bind (("s-0" . 'default-text-scale-reset)
           ("s--" . 'default-text-scale-decrease)
           ("s-=" . 'default-text-scale-increase)))
#+end_src

** Basic Interface Settings
Settings related to built-in enhancements of the UI that do not depend on any
external package.

*** Disable Menus and Scrollbars
Disable tool bar, menu bar and scroll bar. All three are very ugly and
unnecesary. Might be handy for normies, but not for chad Emacs users. Basically
this turns off al the mouse interface.
#+begin_src elisp
  (tool-bar-mode 0)
  (menu-bar-mode 0)
  (scroll-bar-mode 0)
  (tooltip-mode 0)
#+end_src

*** Remove Startup Screen
Inhibit lame startup screen.
#+begin_src elisp
  (setq inhibit-splash-screen t)
  (setq inhibit-startup-message t)
#+end_src

*** Highlight Cuurrent Line
Pretty nice UI enhancement for finding current line. It does not look good in
the terminal version of Emacs, though, so it's not set there.
#+begin_src elisp
  (when window-system (global-hl-line-mode t))
#+end_src

** Transparency Alpha
Functions for toggling transparency alpha.
#+begin_src elisp
  ;; (add-to-list 'default-frame-alist '(alpha . (93 . 84)))
  (defun dalvrosa/toggle-transparency ()
    "Toggle transparency on and off."
    (interactive)
    (let ((alpha (frame-parameter nil 'alpha)))
      (if (eq
           (if (numberp alpha)
               alpha
             (cdr alpha)) ; may also be nil
           100)
          (set-frame-parameter nil 'alpha '(93 . 84))
        (set-frame-parameter nil 'alpha '(100 . 100)))))
  (define-key global-map (kbd "C-c t") 'dalvrosa/toggle-transparency)
#+end_src

** Distraction Free Mode
Olivetti is a simple Emacs minor mode for a nice writing environment, that sets
a desired text body width to automatically resize window margins to keep the
text comfortably in the middle of the window. I use the default text body
width, that is =fill-colum= +2.
#+begin_src elisp
  (use-package olivetti
    :config
    (setq-default olivetti-body-width (+ fill-column 8))
    :bind ("C-c o" . 'olivetti-mode))
#+end_src

This mode can be enabled with =olivetti-mode= in any buffer (everything it does
is buffer-local). And the, it has some commands:
- Shrink =C-c { { { ...=
- Expand =C-c } } } ...=
- Set width =C-c |=

* Programming Configuration
Configuration settings related to programming languages.

** General Configuration
Programming configuration that is not specific to one programming language.
*** Code Completion
Company is a text/code completion framework for Emacs. The name stands for
"complete anything". It uses pluggable back-ends and front-ends to retrieve and
display completion candidates.
#+begin_src elisp
  (use-package company
    :config
    (setq company-show-quick-access  t)
    (setq company-idle-delay 0.0)
    (setq company-minimum-prefix-length 1)
    :hook (prog-mode . company-mode))
#+end_src

Completion will start automatically after you type a few letters. Use =C-n= and
=C-p= to select, =RET= to complete or =TAB= to complete the common part. Search
through the completions with =C-s=, =C-r= and =C-o=. Press =M-(digit)= to
quickly complete with one of the first 10 candidates.

When the completion candidates are shown, press =<f1>= to display the
documentation for the selected candidate, or =C-w= to see its source.
*** Tree Sitter
Tree-sitter is a parser generator tool and an incremental parsing library. It
can build a concrete syntax tree for a source file and efficiently update the
syntax tree as the source file is edited. Watch this [[https://www.thestrangeloop.com/2018/tree-sitter---a-new-parsing-system-for-programming-tools.html][video]] for more
information.
#+begin_src elisp
  (use-package tree-sitter
    :config
    (global-tree-sitter-mode)
    (add-hook 'tree-sitter-after-on-hook #'tree-sitter-hl-mode))

  (use-package tree-sitter-langs
    :after tree-sitter)
#+end_src

*** Terminal Emulator
Emacs-libvterm (vterm) is fully-fledged terminal emulator inside GNU Emacs
based on libvterm, a C library. As a result of using compiled code (instead of
elisp), emacs-libvterm is fully capable, fast, and it can seamlessly handle
large outputs.
#+begin_src elisp
  (use-package vterm
    :config
    (setq vterm-max-scrollback 10000))
#+end_src

*** Language Server Protocol
The goal of the language server protocol is to allow programming language
support to be implemented and distributed independently of any given editor.
#+begin_src elisp
  (use-package lsp-mode
    :config
    (setq lsp-idle-delay 0.1)
    :hook ((c-mode-common . lsp)
           (java-mode . lsp)
           (ruby-mode . lsp)
           (python-mode . lsp)
           (lsp-mode . lsp-enable-which-key-integration))
    :commands lsp)

  (use-package lsp-ui :commands lsp-ui-mode)
#+end_src

Add integration with =treemacs= package.
#+begin_src elisp
  (use-package lsp-treemacs
    :commands lsp-treemacs-errors-list
    :config
    (lsp-treemacs-sync-mode 1))
#+end_src

*** COMMENT Debug Adapter Protocol
*Disclaimer* I'm not tangling the block to follow.

Emacs client/library for Debug Adapter Protocol is a wire protocol for
communication between client and Debug Server. It's similar to the LSP but
provides integration with debug server.
#+begin_src elisp
  (use-package dap-mode
    :after lsp-mode)
#+end_src

*** Line Numbers
Display line numbers.
#+begin_src elisp
  (add-hook 'prog-mode-hook 'display-line-numbers-mode)
#+end_src

*** Tabs vs Spaces
A good programmer knows that spaces are by far a better alternative than tabs.
This doesn't mean you need to insert the spaces manually. Emacs will take care
of this for you.

Configure default tab width to 2 spaces.
#+begin_src elisp
  (setq-default tab-width 2)
#+end_src

Ensure spaces are entered when pressing the =TAB= key.
#+begin_src elisp
  (setq-default indent-tabs-mode nil)
#+end_src

*** Trailing Whitespace
Delete (nasty) trailing whitespace when saving a file (this includes emtpy
lines at the end of the file and empty spaces at the end of lines).
#+begin_src elisp
  (add-hook 'before-save-hook 'delete-trailing-whitespace)
#+end_src

*** Syntax Checking
Flycheck is a modern on-the-fly syntax checking extension for GNU Emacs,
intended as replacement for the older Flymake extension which is part of GNU
Emacs. It uses various syntax checking and linting tools to automatically check
the contents of buffers while you type, and reports warnings and errors
directly in the buffer, or in an optional error list.

#+begin_src elisp
  (use-package flycheck
    :init
    (add-hook 'prog-mode-hook 'flycheck-mode))
#+end_src

Add buffer local Flycheck checkers after LSP for different major modes, from
[[https://github.com/flycheck/flycheck/issues/1762#issuecomment-749789589][this issue]].
#+begin_src elisp
  (defvar-local my-flycheck-local-cache nil)
  (defun my-flycheck-local-checker-get (fn checker property)
    ;; Only check the buffer local cache for the LSP checker, otherwise we get
    ;; infinite loops.
    (if (eq checker 'lsp)
        (or (alist-get property my-flycheck-local-cache)
            (funcall fn checker property))
      (funcall fn checker property)))
  (advice-add 'flycheck-checker-get
              :around 'my-flycheck-local-checker-get)
  (add-hook 'lsp-managed-mode-hook
            (lambda ()
              (when (derived-mode-p 'haskell-mode)
                (setq my-flycheck-local-cache '((next-checkers . (haskell-hlint)))))))
  (add-hook 'lsp-managed-mode-hook
            (lambda ()
              (when (derived-mode-p 'c++-mode)
                (setq my-flycheck-local-cache '((next-checkers . (c/c++-clang-tidy)))))))
  (add-hook 'lsp-managed-mode-hook
            (lambda ()
              (when (derived-mode-p 'sh-mode)
                (setq my-flycheck-local-cache '((next-checkers . (sh-shellcheck)))))))
  (add-hook 'lsp-managed-mode-hook
            (lambda ()
              (when (derived-mode-p 'tex-mode)
                (setq my-flycheck-local-cache '((next-checkers . (tex-chktex)))))))
#+end_src

*** Projectile
Projectile is a project interaction library for Emacs. Its goal is to provide a
nice set of features operating on a project level without introducing external
dependencies (when feasible). It is very nice, because it integrates seamlessly
with version control (like Git) and takes into account =.gitignore= files.
#+begin_src elisp
  (use-package projectile
    :demand t
    :config (projectile-mode +1)
    (setq projectile-ignored-projects '("~/"))
    (setq projectile-project-search-path '(("~/workplace/" . 3)))
    (setq projectile-completion-system 'ivy)
    :bind (:map projectile-mode-map
                ("C-c p" . 'projectile-command-map)
                ("s-r" . 'projectile-command-map)))
#+end_src

Some of the projectile features:
- Jump to a file in project
- Jump to files at point in project
- Jump to a directory in project
- Jump to a file in a directory
- Jump to a project buffer
- Jump to a test in project
- Toggle between files with same names but different extensions (e.g. .h <->
  .c/.cpp, Gemfile <-> Gemfile.lock)
- Toggle between code and its test (e.g. main.service.js <->
  main.service.spec.js)
- Jump to recently visited files in the project
- Switch between projects you have worked on
- Kill all project buffers
- Replace in project
- Multi-occur in project buffers
- Grep in project
- Regenerate project etags or gtags (requires ggtags).
- Visit project in dired
- Run make in a project with a single key chord
- Check for dirty repositories
- Toggle read-only mode for the entire project

I like to use The Silver Searcher for searching instead of standard grep
mainly because is way faster.
#+begin_src elisp
  (use-package ag)
#+end_src

*** Magit
Magit is one of the killer features in Emacs, is a complete text-based user
interface to Git. It fills the glaring gap between the Git command-line
interface and various GUIs, letting you perform trivial as well as elaborate
version control tasks with just a couple of mnemonic key presses. Magit looks
like a prettified version of what you get after running a few Git commands but
in Magit every bit of visible information is also actionable to an extent that
goes far beyond what any Git GUI provides and it takes care of automatically
refreshing this output when it becomes outdated. In the background Magit just
runs Git commands and if you wish you can see what exactly is being run, making
it possible for you to learn the git command-line by using Magit.
#+begin_src elisp
  (use-package magit
    :bind ("C-c g" . 'magit-status))
#+end_src

Using Magit for a while will make you a more effective version control user.
Magit supports and streamlines the use of Git features that most users and
developers of other Git clients apparently thought could not be reasonably
mapped to a non-command-line interface. Magit is both faster and more intuitive
than either the command line or any GUI and these holds for both Git beginners
and experts alike.

*** Git Link
Interactive Emacs functions that create URLs for files and commits in
GitHub/Bitbucket/GitLab/... repositories.

- =git-link= returns the URL for the current buffer's file location at the
  current line number or active region.
- =git-link-commit= returns the URL for the commit at point.
- =git-link-homepage= returns the URL for the repository's homepage.

URLs are added to the kill ring.
#+begin_src elisp
  (use-package git-link
    :demand t
    :config
    (global-set-key (kbd "C-c w l") 'git-link)
    (global-set-key (kbd "C-c w c") 'git-link-commit)
    (global-set-key (kbd "C-c w h") 'git-link-homepage))
#+end_src

*** Snippets
YASnippets is a template system for Emacs. It allows you to type an
abbreviation and automatically expand it into function templates.
#+begin_src elisp
  (use-package yasnippet
    :config
    (use-package yasnippet-snippets)
    (yas-reload-all)
    :hook (prog-mode . yas-minor-mode))
#+end_src

Custom snippets are located in =.config/emacs/snippets= directory (best way to
create them is using =yas-new-snippet= function).

*** Ediff
Ediff provides a convenient way for simultaneous browsing through the
differences between a pair (or a triple) of files or buffers. The files being
compared, are shown in separate windows and the differences are highlighted as
you step through them. You can also copy difference regions from one buffer to
another (and recover old differences if you change your mind). Another powerful
feature is the ability to merge a pair of files into a third buffer.

Don't open new frame for setup window and prefer splitting horizontally.
#+begin_src elisp
  (setq ediff-window-setup-function 'ediff-setup-windows-plain)
  (setq ediff-split-window-function 'split-window-horizontally)
#+end_src

*** File Tree
Treemacs is a file and project explorer that shows the file system outlines of
your projects in a simple tree laout allowing quick navigation and exploration.
I like to use it with the icons from all-the-icons package, so make sure to
have that installed.
#+begin_src elisp
  (use-package treemacs
    :config
    (add-hook 'treemacs-mode-hook  (lambda () (setq-local truncate-lines t)))
    :bind
    (:map global-map
          ("M-0"       . treemacs-select-window)
          ("C-x t 1"   . treemacs-delete-other-windows)
          ("C-x t t"   . treemacs)
          ("C-x t d"   . treemacs-select-directory)
          ("C-x t B"   . treemacs-bookmark)
          ("C-x t C-t" . treemacs-find-file)
          ("C-x t M-t" . treemacs-find-tag)))

  (use-package treemacs-all-the-icons
    :demand t
    :config
    (setq treemacs-indentation 1)
    (treemacs-load-theme "all-the-icons"))
#+end_src

*** Ansi Color
Enable colors.
#+begin_src elisp
  (defun dalvrosa/colorize-compilation ()
    "Colorize from `compilation-filter-start' to `point'."
      (ansi-color-apply-on-region
       compilation-filter-start (point)))
  (add-hook 'compilation-filter-hook #'dalvrosa/colorize-compilation)
#+end_src

** Amazon Specific
Work related configuration: specific to Amazon. There is the EmacsAmazonLibs
package in GitFarm

Amazon developers have created a couple of libraries to make life easier for
developers.

To take advantage of these libraries, create a desktop workspace with the
EmacsAmazonLibs Brazil package. Build the package with =brazil-build release=.
This will create a TAR file in the build directory, such as
=emacs-amazon-libs-20180828094910.tar=. Run the command =M-x
package-install-file= and enter the TAR file when prompted. This will install
EmacsAmazonLibs as an Emacs package in your =~/.emacs.d/elpa= directory. You
should then require the =amz-common= package to pick up the customisation.

#+begin_src elisp
  (if dalvrosa/at-work
      (require 'amz-common))
#+end_src

** C/C++
Configuration for C/C++ programming language. It's mainly done with LSP.

Follow Google C/C++ [[https://google.github.io/styleguide/cppguide.html][coding styles]], who doesn't these days?
#+begin_src elisp
  (use-package google-c-style
    :hook
    (c-mode-common . google-set-c-style)
    (c-mode-common . google-make-newline-indent))
#+end_src

*** Clang Tidy
clang-tidy is a clang-based C++ “linter” tool. Its purpose is to provide an
extensible framework for diagnosing and fixing typical programming errors, like
style violations, interface misuse, or bugs that can be deduced via static
analysis. clang-tidy is modular and provides a convenient interface for writing
new checks.
#+begin_src elisp
  (use-package flycheck-clang-tidy
    :after flycheck
    :hook
    (flycheck-mode . flycheck-clang-tidy-setup))
#+end_src

*** CMake Mode
Use CMake mode for editing CMakeLists.txt files.
#+begin_src elisp
  (use-package cmake-mode)
#+end_src

** TypeScript
Add typescript mode.
#+begin_src elisp
  (use-package typescript-mode
    :demand t
    :config
    (add-to-list 'auto-mode-alist '("\\.tsx\\'" . typescript-mode)))
#+end_src

** Python
Elpy is the Emacs Python Development Environment. It aims to provide an easy to
install, fully-featured environment for Python development.
#+begin_src elisp
  (use-package elpy
    ;; :init
    ;; (elpy-enable)
    )
#+end_src

Once installed, Elpy will automatically provide code completion, syntax error
highlighting and code hinting (in the modeline) for python files. Elpy offers a
lot of features, but the following keybindings should be enough to get started:
- =C-c C-c= evaluates the current python script (or region if something is
selected) in an interactive python shell. The python shell is automatically
displayed aside of your script.
- =C-RET= evaluates the current statement (current line plus the following
  nested lines).
- =C-c C-z= switches between your script and the interactive shell.
- =C-c C-d= displays documentation for the thing under cursor. The documentation
  will pop in a different buffer, that can be closed with q.

Moving around and indenting is similar to Org mode.

Some more things that are pretty sweet.
- =M-.= go to definition.
n- =M-*= go back from definition where I was.
- =C-c C-o= occur definition. All places where a function/class is used.

** Java
Awesome package for LSP integration with Java.
#+begin_src elisp
  (use-package lsp-java)
#+end_src

** Web Development
Web mode is an autonomous emacs major-mode for editing web templates. HTML
documents can embed parts (CSS/JavaScript) and blocks (client/server side).
#+begin_src elisp
  (use-package web-mode
    :config
    (add-to-list 'auto-mode-alist '("\\.phtml\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.tpl\\.php\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.[agj]sp\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.as[cp]x\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.erb\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.mustache\\'" . web-mode))
    (add-to-list 'auto-mode-alist '("\\.djhtml\\'" . web-mode))
    (setq web-mode-markup-indent-offset 2))
#+end_src

Some useful commands:
- Jumping with =C-c C-n= between opening/closing HTML tags or control blocks.
- Code folding with =C-c C-f= for HTML elements and control blocks.
- Snippet insertion with =C-c C-s= (auto indented, aware of text selection, engine
  specific).
- Clever selection and expansion with =C-c C-m=.

** Octave/Matlab
Open Matlab files (.m) in Octave mode by default.
#+begin_src elisp
  (setq auto-mode-alist
        (cons '("\\.m$" . octave-mode) auto-mode-alist))
#+end_src

** R
Use ESS package ("Emacs Speaks Statistics") for editing R code.
#+begin_src elisp
  (use-package ess)
#+end_src

Some useful commands:
- =C-c C-b= to eval buffer.
- =C-c C-j= to eval line.
- =C-c C-r= to eval region.
- =C-c C-f= to eval function.

* Org Mode
Configuration for Org mode, one of the best major modes (org mode is definately
an Emacs killer feature). Org mode is for keeping notes, maintaining to-do
lists, planning projects, authoring documents, computational notebooks,
literate programming and more-—in a fast and effective plain text system. I
mainly use it for organize my life, literate programming and even writting
emails.

** Speed Commands
Activate single letter commands at beginning of a headline.
#+begin_src elisp
  (setq org-use-speed-commands t)
#+end_src

This is very useful for navigating/modifying org mode files. I usually use
=n/p= for next/previous, =b/f= for backward/forward, and =l/r= for left/right.

** Refile
Configuration for refiling command =C-c C-w=.
#+begin_src elisp
  (setq org-refile-targets '((nil :maxlevel . 2)
                                  (org-agenda-files :maxlevel . 2)))
  (setq org-outline-path-complete-in-steps nil)
  (setq org-refile-use-outline-path 'file)
#+end_src

** Capture Templates
Take notes quickly within Emacs with =C-c c=.
#+begin_src elisp
  (setq org-default-notes-file "~/Documents/Agenda.org")
  (define-key global-map (kbd "C-c c") 'org-capture)
#+end_src

Set my custom capture templates. Brief description of them:
- Task: basic template for TODO tasks. This is inside my =Agenda.org= file under
  "Refile" heading. Then I manually refile this tasks under a particular
  projects or into a "Standalone Tasks" heading.
- Text Note: template for taking quick notes. This is inside my =Notes.org=
  file.
#+begin_src elisp
  (setq org-capture-templates
        '(("t" "Task" entry
           (file+olp "~/Documents/Agenda.org" "Refile")
           "* TODO [#C] %?\n%a\n%i" :empty-lines 1)
          ("n" "Text Note" entry
           (file+olp "~/Documents/Notes.org" "Refile")
           "* %?" :empty-lines 1)))
#+end_src

For the above email (or link) capture templates to work with =mu4e= (email
client used in this configuration), it is needed the following.
#+begin_src elisp
  (require 'org-mu4e)
#+end_src

Add some global key binding for storing link =C-c l= and jumping (goto) to
current clocked task.
#+begin_src elisp
  (global-set-key (kbd "C-c l") 'org-store-link)
  (global-set-key (kbd "C-c j") 'org-clock-goto)
#+end_src

** State Keywords
Custom todo keywords. I use them as follows:
  - TODO: task that needs to be done (in order, depending priority).
  - WAIT: I am waiting for something that does not depend on myself (i.e,
  something external).
  - NEXT: todo task, that is on-going or is next task to be done.
  - DONE: task is done.
  - CANCELLED: task is cancelled.
#+begin_src elisp
  (setq org-todo-keywords
        '((sequence "TODO(t!)" "WAIT(w!)" "NEXT(n!)" "|"
                    "DONE(d!)" "CANCELLED(c!)")))
#+end_src

For tracking TODO state changes, the meaning of the characters is as follows
(check [[https://orgmode.org/manual/Tracking-TODO-state-changes.html][Tracking TODO state changes]] for more information): ! for timestamp and @
for note with timestamp. I'm not using automatic notes anymore, those can be
recovered by prefixing state change with =C-u=.

Log state changes into drawer.
#+begin_src elisp
  (setq org-log-into-drawer t)
#+end_src

** Code Evaluation
Allow code blocks in these languages to be evaluated with =C-c C-c=.
#+begin_src elisp
  (eval-after-load "org"
        (org-babel-do-load-languages
         'org-babel-load-languages
         '((C . t)
           ;; (C++ . t)
           (python . t)
           (latex . t)
           (matlab . t)
           (shell . t)
           (css . t)
           (calc . t)
           (R . t)
           (js . t))))
#+end_src

Don't ask for confirmation for evaluating code.
#+begin_src elisp
  (setq org-confirm-babel-evaluate nil)
#+end_src

** Interface
Restore windows after quitting org agenda.
#+begin_src elisp
  (setq org-agenda-restore-windows-after-quit t)
#+end_src

*** List Configuration
Allow alphabetical list, i.e., list like "a)", "A" or "A)" as element lists.
#+begin_src elisp
  (setq org-list-allow-alphabetical t)
#+end_src

*** Indent Mode
Launch org mode in indent mode.
#+begin_src elisp
  (setq org-startup-indented t)
#+end_src

*** Initial Visibility
When Emacs first visits an Org file, set the global state to =content=, to show
only headers.
#+begin_src elisp
  (setq org-startup-folded 'content)
#+end_src

** Agenda
Set =C-c a= for opening org agenda. This is one of the features that I like the
most of Emacs. It's great.
#+begin_src elisp
  (global-set-key (kbd "C-c a") 'org-agenda)
#+end_src

Set files to show in agenda.
#+begin_src elisp
  (setq org-agenda-files '("~/Documents/Agenda.org"))
#+end_src

Custom agenda views. I use a custom view that contains the following blocks:
  - Agenda for the current day (including habits).
  - NEXT tasks (that are not scheduled).
  - Tasks in the refile tree (waiting to be refiled).
  - Work related tasks.
  - Project tasks (i.e., tasks with :PROJ: tag). Only those that are not
    schedule.
  - Rest of standalone tasks. Also only those that are not schedule.
  - WAIT tasks.
#+begin_src elisp
  (setq org-agenda-custom-commands
        '((" " "Block Agenda"
           ((agenda "" ((org-agenda-span 1)))
            (todo "NEXT"
                  ((org-agenda-overriding-header "Next Actions")
                   (org-agenda-skip-function
                    '(org-agenda-skip-entry-if 'scheduled))))
            (tags-todo "+REFILE" ((org-agenda-overriding-header "Refile")))
            (tags-todo "TODO=\"TODO\"+AMZN"
                       ((org-agenda-overriding-header "Amazon")
                        (org-agenda-skip-function
                         '(org-agenda-skip-entry-if 'scheduled))))
            (tags-todo "TODO=\"TODO\"+PROJ"
                       ((org-agenda-overriding-header "Projects")
                        (org-agenda-skip-function
                         '(org-agenda-skip-entry-if'scheduled))))
            (tags-todo "TODO=\"TODO\"+SING"
                       ((org-agenda-overriding-header "Standalone Tasks")
                        (org-agenda-skip-function
                         '(org-agenda-skip-entry-if 'scheduled))))
            (todo "WAIT" ((org-agenda-overriding-header "Waiting"))))
           ((org-agenda-start-with-log-mode t)))))
#+end_src

While in this agenda view press =w= for changing the agenda span to full week
or =d= for just showing curren day. Also, it is posible to toggle log mode with
=l=.

Show all posible items that should in agenda log mode.
#+begin_src elisp
  (setq org-agenda-log-mode-items '(closed clock state))
#+end_src

Set deadline warning days to 7 (instead of the default 14 days).
#+begin_src elisp
  (setq org-deadline-warning-days 7)
#+end_src

With =q= bury agenda instead of killing it (faster of course).
#+begin_src elisp
  (setq org-agenda-sticky t)
#+end_src

I don't like empty blocks to show in customized agenda, so I hide them (this
code is from [[https://lists.gnu.org/archive/html/emacs-orgmode/2015-06/msg00266.html][emacs mail list]]).
#+begin_src elisp
  (defun dalvrosa/org-agenda-delete-empty-blocks ()
    "Remove empty agenda blocks.
    A block is identified as empty if there are fewer than 2
    non-empty lines in the block (excluding the line with
    `org-agenda-block-separator' characters)."
    (when org-agenda-compact-blocks
      (user-error "Cannot delete empty compact blocks"))
    (setq buffer-read-only nil)
    (save-excursion
      (goto-char (point-min))
      (let* ((blank-line-re "^\\s-*$")
             (content-line-count (if (looking-at-p blank-line-re) 0 1))
             (start-pos (point))
             (block-re (format "%c\\{10,\\}" org-agenda-block-separator)))
        (while (and (not (eobp)) (forward-line))
          (cond
           ((looking-at-p block-re)
            (when (< content-line-count 2)
              (delete-region start-pos (1+ (point-at-bol))))
            (setq start-pos (point))
            (forward-line)
            (setq content-line-count (if (looking-at-p blank-line-re) 0 1)))
           ((not (looking-at-p blank-line-re))
            (setq content-line-count (1+ content-line-count)))))
        (when (< content-line-count 2)
          (delete-region start-pos (point-max)))
        (goto-char (point-min))
        ;; The above strategy can leave a separator line at the beginning
        ;; of the buffer.
        (when (looking-at-p block-re)
          (delete-region (point) (1+ (point-at-eol))))))
    (setq buffer-read-only t))

  (add-hook 'org-agenda-finalize-hook #'dalvrosa/org-agenda-delete-empty-blocks)
#+end_src

*** Habits
Show a nice graph for recurring habits. See [[https://orgmode.org/manual/Tracking-your-habits.html][Tracking your Habits]] in the
Org-mode manual for more information.
#+begin_src elisp
  (require 'org-habit)
#+end_src

Configure the column the absolute column at which to insert habit consistency
graphs (consistency graph overwrites anything else in the buffer).
#+begin_src elisp
  (setq org-habit-graph-column 55)
#+end_src

*** Notifications
Enable notifications for scheduled tasks.
#+begin_src elisp
  (require 'appt)
  (setq appt-time-msg-list nil)
  (setq appt-message-warning-time '15
        appt-display-interval '5)

  (setq appt-display-mode-line nil
        appt-display-format 'window)
  (appt-activate 1)

  (org-agenda-to-appt)
  (run-at-time "24:01" 1800 'org-agenda-to-appt)
  (add-hook 'org-finalize-agenda-hook 'org-agenda-to-appt)

  (defun dalvrosa/appt-send-notification (title msg)
    (if dalvrosa/at-work
        (shell-command (concat "terminal-notifier"
                               " -message " msg
                               " -title " title
                               " -sender " "org.gnu.Emacs"))
      (shell-command (concat "notify-send " msg " " title))))

  (defun dalvrosa/appt-display (min-to-app new-time msg)
    (dalvrosa/appt-send-notification
     (format "'Meeting in %s minutes'" min-to-app)
     (format "'%s'" msg)))
  (setq appt-disp-window-function (function dalvrosa/appt-display))
#+end_src

** Archive
Set default org archive location.
#+begin_src elisp
  (setq org-archive-location "::* Archived Items")
#+end_src

Use lowercase tag names.
#+begin_src elisp
  (setq org-archive-tag "archive")
#+end_src

** Table of Contents
It’s nice to have a table of contents section (without exporting) for long
literate configuration files (like this one!) so I use org-toc-org to
automatically update the ToC in any header with a property named TOC.
#+begin_src elisp
  (use-package toc-org
    :hook (org-mode . toc-org-mode))
#+end_src

The default ToC depth is 2. Is possible to change this with the following tag
format =:TOC_3:= for seting the max depth of the headlines in the table of
contents to 3.

* Markup Languages
Section for markup languages.

** Markdown Mode
Major mode for editting Markdown-formatted text.
#+begin_src elisp
  (use-package markdown-mode)
#+end_src

** TeX/LaTeX
AUCTeX is an extensible package for writing and formatting TeX files in GNU
Emacs. It supports many different TEX macro packages, including AMS-TeX, LaTeX,
Texinfo, ConTeXt, and docTeX (dtx files).
#+begin_src elisp
  (use-package latex
    :ensure auctex
    :config
    ;; Always in math mode
    (add-hook 'LaTeX-mode-hook 'LaTeX-math-mode)
    ;; Set PDF viewer to pdf-tools with correlation
    (setq TeX-view-program-selection '((output-pdf "PDF Tools")))
    (add-hook 'LaTeX-mode-hook 'TeX-source-correlate-mode)
    ;; Enable electric behavior.
    (setq TeX-electric-math t)
    (setq TeX-electric-sub-and-superscript t)
    ;; I want \items indented.
    (setq LaTeX-item-indent 0)
    :bind (
           :map LaTeX-mode-map
           ;; Command for cleaning auxiliary files
           ("C-x M-k" . 'TeX-clean)))
#+end_src

AUCTex is not just great, it's the best framework for writting LaTeX. This are
the commands I use the most:
- =C-c C-e= for inserting environment (figure, equation, table, etc.).
- =C-c C-m= for inserting macro (documentclass, usepackage, frac, etc.).
- =C-c C-s= for inserting section (chapter, section, subsection, etc.).
- =C-f- C-f C-*= is a font specifier (e.g., =C-c C-f C-b= inserts bold face).
  Very useful when region is active. For deleting (innermost) font
  specification use =C-c C-f C-d=.
- =C-c ]= for ending current environment.
- =C-M-a= and =C-M-e= for moving point to begin or end of current environment.
- =` *= for entering mathematical symbols (e.g., =` a= for inserting =\alpha=).
- =C-c .= for marking current environment and =C-c *= for marking current
  section.

*** RefTeX
RefTeX is a specialized package for support of labels, references, citations,
and the index in LaTeX. RefTeX wraps itself round four LaTeX macros: =\label=,
=\ref=, =\cite=, and =\index=. Using these macros usually requires looking up
different parts of the document and searching through BibTeX database files.
RefTeX automates these time-consuming tasks almost entirely. It also provides
functions to display the structure of a document and to move around in this
structure quickly.
- =C-c == show ToC.
- =C-c (= create label.
- =C-c )= reference label.
- =C-c [= create cite.
- =C-c /= for creating an index.
#+begin_src elisp
  (setq reftex-plug-into-AUCTeX t)
  (setq reftex-toc-split-windows-fraction 0.2)
#+end_src

*** Compilation
Compile always with =-shell-escape= flag. I usually need this for PGF/TikZ or
Minted.
#+begin_src elisp
  (setq TeX-command-extra-options "-shell-escape -synctex=1")
#+end_src

Don't ask for confirmation when saving and update output PDF after compilation.
#+begin_src elisp
  (setq TeX-save-query nil)
  (add-hook 'TeX-after-compilation-finished-functions
            #'TeX-revert-document-buffer)
#+end_src

*** BibTeX
For managing .bib files. Use =C-c C-c=. Insert entries templates with different
key bindings. Also, =C-c C-f= for inserting new field.
#+begin_src elisp
  (setq bibtex-align-at-equal-sign t)
  (setq bibtex-entry-format `(opts-or-alts required-fields
                              numerical-fields whitespace realign
                              last-comma delimiters unify-case
                              braces sort-fields))
  (setq bibtex-autokey-year-title-separator ":")
#+end_src

** Ledger
Ledger is a powerful, double-entry accounting system that is accesed from the
UNIX command line.
#+begin_src elisp
  (use-package ledger-mode)
#+end_src

* PDF Viewer
PDF Tools is, among other things, a replacement of DocView for PDF files. The
key difference is that pages are not pre-rendered by e.g. ghostscript and
stored in the file-system, but rather created on-demand and stored in memory.

Disable here Swiper for searching, and fallback to Isearch, since Swiper
searchs in the source code rather than in the PDF itself.
#+begin_src elisp
  (use-package pdf-tools
    :demand t
    :config
    (pdf-tools-install)
    :bind (:map pdf-view-mode-map
                ("C-s" . 'isearch-forward)))
#+end_src

Slices are really useful:
- =s r= reset slice.
- =s m= set slice with mouse.
- =s b= set slice using bounding box.

Multiple annotations commands are supported with keybind prefix =C-c C-a= (for
introducing is best to use the mouse for selecting position), and for listing
=C-c C-a l=.

For displaying PDF metadata the command is =I=.

Also, is pretty useful to list all lines containing a phrase (this is called
occur), and can be access using =M-s o=.

* Email Management
Configuration for email within Emacs with =mu4e=. The program mu should be
installed, this program is designed to enable super-efficient handling of
e-mail; searching, reading, replying, moving, deleting. Also Isync should be
installed and configured (this program is the same as mbsync, which is the name
of the isync executable). This program will sync email computer <-> server,
check out my [[file:~/.config/isync/README.org][isync configuration]].

Set =C-c e= as default keybinding for email =mu4e=.
#+begin_src elisp
  (require 'mu4e)
  (setq mail-user-agent 'mu4e-user-agent)
  (global-set-key (kbd "C-c e") 'mu4e)
  #+end_src

Configure the completion function.
#+begin_src elisp
  (setq mu4e-completing-read-function 'ivy-completing-read)
#+end_src

Customization of header view. I don't use mailing lists that often, so I have
remove them from headers view.
#+begin_src elisp
  (setq mu4e-headers-fields '((:human-date . 12)
                              (:flags . 6)
                              (:maildir . 18)
                              (:from-or-to . 22)
                              (:subject)))
#+end_src

Set custom mailboxes depending of =dalvrosa/at-work= variable.
#+begin_src elisp
  (if dalvrosa/at-work
      (setq dalvrosa/mailboxes "personal spam amazon")
    (setq dalvrosa/mailboxes "personal spam"))
  (setq mu4e-get-mail-command
        (concat "mbsync -c ~/.config/isync/mbsyncrc -V " dalvrosa/mailboxes))
#+end_src

Prevent mu4e from permanently deleting trashed items. This snippet was taken
from [[http://cachestocaches.com/2017/3/complete-guide-email-emacs-using-mu-and-/][this article]]. Namespaced and added functionality to mark deleted item as
read.
#+begin_src elisp
  (defun dalvrosa/remove-nth-element (nth list)
    (if (zerop nth) (cdr list)
      (let ((last (nthcdr (1- nth) list)))
        (setcdr last (cddr last))
        list)))
  (setq mu4e-marks (dalvrosa/remove-nth-element 5 mu4e-marks))
  (add-to-list 'mu4e-marks
               '(trash
                 :char ("d" . "▼")
                 :prompt "dtrash"
                 :dyn-target (lambda (target msg) (mu4e-get-trash-folder msg))
                 :action (lambda (docid msg target)
                           (mu4e--server-move docid
                                           (mu4e--mark-check-target target) "-N+S-u"))))
#+end_src

** Contexts for Multiple Accounts
I am using contexts for handling two different accounts at the same time. When
launching mu4e for the first time don't ask and just pick the first context
(the default).
#+begin_src elisp
  (setq mu4e-context-policy 'pick-first)
#+end_src

First, lets define basic configuration for all accounts (contexts in mu4e
terminology). This is, set maildir, my personal email addresses (used for not
replying to myself) and the behavior for sent messages.
#+begin_src elisp
  (setq mu4e-sent-messages-behavior 'sent)
  (setq message-signature-file "~/Documents/Signature.txt")
  (setq smtpmail-stream-type 'starttls)
  (setq smtpmail-smtp-service 587)
  (setq mu4e-change-filenames-when-moving t)
#+end_src

Define some variables depending if I'm at work. I'm using an SSH tunnel to
bypass block in 587 port.
#+begin_src elisp
  (if dalvrosa/at-work
      (progn
        (setq dalvrosa/smtp-server "localhost")
        (setq dalvrosa/smtp-port 1587))
    (progn
      (setq dalvrosa/smtp-server "mail.alvarezrosa.com")
      (setq dalvrosa/smtp-port 587)))
#+end_src

The SSH tunnel should be started.
#+begin_src shell :tangle no
  ssh -NTfL 1587:localhost:1587 root@alvarezrosa.com
#+end_src

The actual context accounts.
#+begin_src elisp
  (setq mu4e-contexts
        `( ,(make-mu4e-context
             :name "Personal"
             :match-func (lambda (msg)
                           (when msg
                             (string-match-p "^/Personal" (mu4e-message-field msg :maildir))))
             :vars `(
                     (mu4e-inbox-folder . "/Personal/Inbox")
                     (mu4e-sent-folder . "/Personal/Sent")
                     (mu4e-drafts-folder . "/Personal/Drafts")
                     (mu4e-trash-folder . "/Personal/Trash")
                     (mu4e-refile-folder . "/Personal/Archive")
                     (user-mail-address . "david@alvarezrosa.com")
                     (smtpmail-smtp-service . ,dalvrosa/smtp-port)
                     (smtpmail-smtp-server . ,dalvrosa/smtp-server)))
           ,(make-mu4e-context
             :name "Spam"
             :match-func (lambda (msg)
                           (when msg
                             (string-match-p "^/Spam" (mu4e-message-field msg :maildir))))
             :vars `(
                     (mu4e-inbox-folder . "/Spam/Inbox")
                     (mu4e-sent-folder . "/Spam/Sent")
                     (mu4e-drafts-folder . "/Spam/Drafts")
                     (mu4e-trash-folder . "/Spam/Trash")
                     (mu4e-refile-folder . "/Spam/Archive")
                     (user-mail-address . "davids@alvarezrosa.com")
                     (smtpmail-smtp-service . ,dalvrosa/smtp-port)
                     (smtpmail-smtp-server . ,dalvrosa/smtp-server)))
           ,(make-mu4e-context
             :name "Amazon"
             :match-func (lambda (msg)
                           (when msg
                             (string-match-p "^/Amazon" (mu4e-message-field msg :maildir))))
             :vars '(
                     (mu4e-inbox-folder . "/Amazon/Inbox")
                     (mu4e-sent-folder . "/Amazon/Sent Items")
                     (mu4e-drafts-folder . "/Amazon/Drafts")
                     (mu4e-trash-folder . "/Amazon/Deleted Items")
                     (mu4e-refile-folder . "/Amazon/Archive")
                     (user-mail-address . "dalvrosa@amazon.com")
                     (smtpmail-smtp-server . "ballard.amazon.com")
                     (smtpmail-smtp-service . 1587)))))
#+end_src

** Bookmarks
Add custom bookmark for all inboxes.
#+begin_src elisp
  (add-to-list 'mu4e-bookmarks
               '(:name "All Inboxes"
                :query "maildir:/Personal/Inbox OR maildir:/Amazon/Inbox OR maildir:/Spam/Inbox"
                :key ?i))
#+end_src

** Update and Notification
Redefine update function to always run update command in the background.
#+begin_src elisp
  (defun dalvrosa/mu4e-update-mail-and-index ()
    (interactive)
    (mu4e-update-mail-and-index t))

  (define-key mu4e-main-mode-map (kbd "U") 'dalvrosa/mu4e-update-mail-and-index)
  (define-key mu4e-update-minor-mode-map (kbd "C-c C-u") 'dalvrosa/mu4e-update-mail-and-index)
#+end_src

Let's init mu4e on the background when launching Emacs and set up automatic
email updates every 4 hours.
#+begin_src elisp
  (mu4e t)
  (setq mu4e-update-interval (* 4 60 60))
#+end_src

Use mu4e alert package for displaying unread email in modeline and enable
notifications.
#+begin_src elisp
  (setq doom-modeline-mu4e t)

  (use-package mu4e-alert
    :config
    (mu4e-alert-enable-mode-line-display)
    (mu4e-alert-enable-notifications))
#+end_src

** Queued Email
It allows to save the outgoing mail (output tray) to send them later. Useful
when working without an Internet connection, for example. This can be toggled
in mu4e main view with =m=.
#+begin_src elisp
  (setq smtpmail-queue-dir "~/.local/share/mail/Queue/cur")
#+end_src

** Emacs Unified Directory Client
EUDC is the Emacs Unified Directory Client, a common interface to directory
servers and contact information.

Currently supported back-ends are:
- LDAP, Lightweight Directory Access Protocol
- BBDB, Big Brother’s Insidious Database
- macOS Contacts

The main features of the EUDC interface are:
- Queries using a customizable form
- Inline query expansion (for instance you can expand a name to an email
  address in a mail message buffer using a server as an address book)
- Multiple servers can be tried in turn until a match is found for an inline
  query
- Fast minibuffer queries for email addresses and phone numbers
- Interface to BBDB to let you insert server records into your own BBDB
  database (see BBDB in BBDB Manual)
#+begin_src elisp
  (use-package eudc
    :ensure nil
    :after (ldap bbdb)
    :init (require 'eudc)
    :bind (:map message-mode-map
                (("<M-tab>" . 'eudc-expand-inline)))
    :config
    (eudc-set-server "ldap.amazon.com" 'ldap t)
    (eudc-bbdb-set-server "localhost")
    (setq eudc-server-hotlist
          '(("localhost" . bbdb)
            ("ldap.amazon.com" . ldap)))
    (setq eudc-inline-expansion-servers 'hotlist))
#+end_src

*** Lightweight Directory Access Protocol
LDAP, the Lightweight Directory Access Protocol, is a communication protocol
for directory applications defined in RFC 1777.
#+begin_src elisp
  (use-package ldap
    :ensure nil
    :demand t
    :config
    (setq ldap-default-host "ldap.amazon.com")
    (setq ldap-host-parameters-alist '(("ldap.amazon.com"
                                        base "o=amazon.com"
                                        auth simple))))
#+end_src

*** Big Brother’s Insidious Database
BBDB is the Big Brother’s Insidious Database, a package for Emacs originally
written by Jamie Zawinski which provides rolodex-like database functionality
featuring tight integration with the Emacs mail and news readers. It is often
used as an enhanced email address book.
#+begin_src elisp
  (use-package bbdb
    :demand t)
#+end_src

=bbdb-vcard= imports and exports vCards (version 3.0) as defined in RFC 2425
and RFC 2426 to/from The Insidious Big Brother Database (BBDB). Version 2.1
vCards are converted into version 3.0 on import.
#+begin_src elisp
    (use-package bbdb-vcard
      :after bbdb)
#+end_src

** Composition/Sending
Set default send mail function.
#+begin_src elisp
  (require 'smtpmail)
  (setq message-send-mail-function 'smtpmail-send-it)
#+end_src

Configure citation line (the one that says "On 20 April David wrote...") .
#+begin_src elisp
  (setq message-citation-line-function 'message-insert-formatted-citation-line)
  (setq message-citation-line-format "On %a %d %b %Y at %R, %N wrote:")
#+end_src

Kill message buffer after sending a message.
#+begin_src elisp
  (setq message-kill-buffer-on-exit t)
#+end_src

*** Org Mode HTML
This program sends HTML email using Org-mode HTML export. This approximates a
WYSiWYG HTML mail editor from within Emacs, and can be useful for sending
tables, fontified source code, and inline images in email. LaTeX formulas can
be sent using the modern math delimiters such as \(...\) and \[...\], and also
environments of any kind (the only requirement is that the ‘\begin’ statement
appears on a new line, preceded by only whitespace). It's very useful when
dealing with LaTeX formulas to use =C-c C-x C-l= for producing a preview image
of the LaTeX fragment at point and overlay it over the source code (f there is
no fragment at point, process all fragments in the current entry-—between two
headlines).
#+begin_src elisp
  (use-package org-mime
    :config
    (setq org-mime-export-options '(:with-latex dvipng
                                    :section-numbers nil
                                    :with-author nil
                                    :with-toc nil))
    :bind (:map message-mode-map
                (("C-c o" . 'org-mime-edit-mail-in-org-mode)
                 ("C-c M-o" . 'org-mime-htmlize))))
#+end_src

Some useful functions are =org-mime-edit-mail-in-org-mode= (press =C-c o=) for
editing the mail in a special buffer in Org mode, and then =org-mime-htmilize=
(with =C-c M-o=) from within a mail composition buffer to export either the
entire buffer or just the active region to html, and embed the results into the
buffer as a text/html mime section. After =org-mime-htmlize=, you can always
run =org-mime-revert-to-plain-text-mail= restore the original plain text mail.

For embedding image into mail body the syntax is as follows:
=[[/full/path/to/your.jpg]]=.

The function =org-mime-org-buffer-htmlize= can be called from an Org mode
buffer to export either he whole buffer or the narrowed subtree or active
region to HTML, and open a new email buffer including the resulting HTML
content as an embedded mime section.

The function =org-mime-org-subtree-htmlize= is similar to
=org-mime-org-buffer-htmlize= but works on subtree. It can also read subtree
properties MAIL_SUBJECT, MAIL_TO, MAIL_CC, and MAIL_BCC. Here is the sample of
subtree:
#+begin_example
  ,* mail one
   :PROPERTIES:
   :MAIL_SUBJECT: mail title
   :MAIL_TO: person1@gmail.com
   :MAIL_CC: person2@gmail.com
   :MAIL_BCC: person3@gmail.com
   :END:
  some text here ...
#+end_example

*** COMMENT Org Msg
Configure Org Msg that automatically writes HTML emails when needed.
#+begin_src elisp
  (use-package org-msg
    :after (mu4e)
    :config
    (setq
     org-msg-options "html-postamble:nil num:nil ^:{} toc:nil author:nil email:nil tex:dvipng"
     org-msg-default-alternatives '((new . (text))
                                    (reply-to-html . (text html))
                                    (reply-to-text . (text))))
    (org-msg-mode))
#+end_src

** Attachment Management
Configure attachment directory for downloading.
#+begin_src elisp
  (setq mu4e-attachment-dir "~/Downloads")
#+end_src

For attaching files while composing new email. To attach file, just mark in
dired and =C-c RET C-a=. This is useful for attaching multiple files, but the
default =C-c C-a= command for attaching (in composition buffer) works just
well.
#+begin_src elisp
  (require 'gnus-dired)
  (defun gnus-dired-mail-buffers ()
    "Return a list of active message buffers."
    (let (buffers)
      (save-current-buffer
        (dolist (buffer (buffer-list t))
          (set-buffer buffer)
          (when (and (derived-mode-p 'message-mode)
                   (null message-sent-message-via))
            (push (buffer-name buffer) buffers))))
      (nreverse buffers)))
  (setq gnus-dired-mail-mode 'mu4e-user-agent)
  (add-hook 'dired-mode-hook 'turn-on-gnus-dired-mode)
#+end_src

** Meetings
Allow reply to iCalendar emails (i.e. accept, decline or tentatively accept
meetings).
#+begin_src elisp
  (require 'mu4e-icalendar)
  (mu4e-icalendar-setup)
  (setq mu4e-icalendar-trash-after-reply t)
  #+end_src

Capture meetings and display them in org-agenda.
#+begin_src elisp
  (setq gnus-icalendar-org-capture-file "~/Documents/Agenda.org")
  (setq gnus-icalendar-org-capture-headline '("Calendar"))
  (gnus-icalendar-org-setup)
#+end_src

* Web Feed Reader
Elfeed is an extensible web feed reader for Emacs, supporting both Atom and
RSS. It requires Emacs 24.3.
#+begin_src elisp
  (use-package elfeed
    :bind (("C-c f" . 'elfeed)
           :map elfeed-search-mode-map (("v" . 'dalvrosa/elfeed-play-with-mpv)
                                     ("i" . 'dalvrosa/elfeed-ignore)))
    :config (setq elfeed-db-directory "~/.config/emacs/elfeed"
                  elfeed-search-filter "@1-week-ago -no "
                  elfeed-search-title-max-width 100))
#+end_src

** Elfeed Org
Use Elfeed Org for managing subscriptions to RSS/Atom feeds using an org mode
file.
#+begin_src elisp
  (use-package elfeed-org
    :after elfeed
    :init (elfeed-org)
    :config (setq rmh-elfeed-org-files (list "~/Documents/Subscriptions.org")))
#+end_src

** Elfeed Goodies
This package allows to configure the Elfeed interface.
#+begin_src elisp
  (use-package elfeed-goodies
    :after elfeed
    :init (elfeed-goodies/setup)
    :config
    (setq elfeed-goodies/powerline-default-separator 'utf-8)
    (setq elfeed-goodies/entry-pane-size 0.40))
#+end_src

** Custom Functions
For interacting with video (e.g., Youtube subscriptions). This allows to open
the media with mpv. Just press =v= and the video will be openend with mpv.
#+begin_src elisp
  (defun dalvrosa/elfeed-play-with-mpv ()
    (interactive)
    (setq url (elfeed-entry-link (elfeed-search-selected :single)))
    (start-process "elfeed-mpv" nil "mpv" "--ytdl-format=[height<=720]" url)
    (elfeed-search-untag-all-unread))
#+end_src

Ignore current entry (i.e., tag it with =no= TAG).
#+begin_src elisp
  (defun dalvrosa/elfeed-ignore ()
    (interactive)
    (setq entry (elfeed-search-selected :single))
    (setq tag (intern "no"))
    (elfeed-tag entry tag)
    (elfeed-search-update-entry entry)
    (forward-line))
#+end_src

* Internet Relay Chat (IRC)
IRC is a text-based chat system. It enables discussions among any number of
participants in so-called conversation channels, as well as discussions between
only two partners--for example, in question-and-answer dialogues. This protocol
is actually older than the World Wide Web. There are several built-in Emacs IRC
clients, but I use the default ERC, that I think is the best one. I have bind
the ERC client, with TLS support, to =C-c i= ("i" for IRC).
#+begin_src elisp
  (global-set-key (kbd "C-c i") 'erc-tls)

  (if dalvrosa/at-work
      (setq erc-server "localhost")
    (setq erc-server "irc.alvarezrosa.com"))
  (setq erc-nick "dalvrosa")
  (setq erc-user-full-name "David Álvarez Rosa")
  (setq erc-prompt-for-password nil)
#+end_src

If you are an IRC user, you may find me out there. I usually hang out in
libera.chat these days (after the freenode management debacle) with the
=dalvrosa= nick.

I have configured the authentication file (see [[*Authentication File][Authentication File]]) with
password information so I don't need to enter it every time. I am actually
connecting to a personal IRC Bouncer (ZNC) hosted in my personal VPS at
irc.alvarezrosa.com, so I do most of the configuration there. The line added to
this file looks as follows:
#+begin_src conf :tangle no
  machine irc.alvarezrosa.com port 6697 login dalvrosa password david:***pass***
  machine localhost port 6697 login dalvrosa password david:***pass***
#+end_src

I'm using SSH tunneling to bypass port 6697 block. This can be done with the
following command.
#+begin_src shell :tangle no
  ssh -NTfL 6697:localhost:6697 root@alvarezrosa.com
#+end_src

After quiting server, query or parting a channel I want the buffer to be killed
automatically.
#+begin_src elisp
  (setq erc-kill-buffer-on-part t)
  (setq erc-kill-queries-on-quit t)
  (setq erc-kill-server-buffer-on-quit t)
#+end_src

Some minor tweaks for displaying (and buffer) configuration.
#+begin_src elisp
  (setq erc-fill-function 'erc-fill-static)
  (setq erc-fill-static-center 22)
  (setq erc-rename-buffers t)
#+end_src

I use ERC channel tracking (it is enabled by default). Basically, show in the
modeline unread chat messages from current openend channels and use =C-c C-SPC=
repeatedly for switching between them (in *any* mode).
#+begin_src elisp
  (setq erc-track-exclude-types '("JOIN" "NICK" "PART" "QUIT" "MODE" "AWAY"))
  (setq erc-track-exclude-server-buffer t)
#+end_src

** Nickname Highlighter
This package will auto colorize (highglight) nicknames, ignoring certain
characters that IRC clients add to nicknames to avoid duplicates (nickname,
nickname’, nickname”, etc.)
#+begin_src elisp
  (use-package erc-hl-nicks)
#+end_src

* Useful Resources
- [[https://github.com/daedreth/UncleDavesEmacs][Dawid Eckert]] has a Youtube channel with nice Emacs tutorials (is called
  UncleDave).
- [[https://github.com/mxco86/emacs-config/blob/master/.emacs.d/emacs.org][Matthew Ryall]]
- [[https://github.com/daviwil/dotfiles/blob/master/Emacs.org][David Wilson]] is the owner of SystemCrafters channel.
- [[https://github.com/munen/emacs.d][Alain M. Lafon]] is the author of "Play Emacs like an instrument" video, and
  has a very neat configuration.
- [[https://gitlab.com/protesilaos/dotfiles/-/blob/master/emacs/.emacs.d/prot-emacs.org][Protesilaos Stavrou]] this guy is amazing, he also has a youtube channel.
- [[https://github.com/bradwright/emacs.d][Bradley Wright]]
- [[https://pages.sachachua.com/.emacs.d/Sacha.html][Sacha Chua ]]she is a well known Emacs blogger.
- [[https://github.com/emacs-tw/awesome-emacs][Awesome Emacs]] is a community driven list of useful Emacs packages, utilities
  and libraries
- [[http://doc.norang.ca/org-mode.html][Bernt Hansen]] has an amazing Org mode configuration called "Organize Your Life
  In Plain Text!" and it's probably the most complete configuration out there.
- [[https://github.com/SirPscl/emacs.d][Pascal Huber]] another nice Emacs configuration to take into account.
- [[https://cestlaz.github.io/stories/emacs/][Mike Zamansky]] a well known Emacs guru, that has a blog called "C'est la Z"
  with a nice Emacs series (+75 blog entries, each one of them with video).
- [[https://github.com/rememberYou/.emacs.d/blob/master/config.org/][Terencio Agozzino]] is a computer engineer with a very nice (literate) Emacs
  configuration. He has written in Reddit (as =u/rmberYou/=) several Emacs
  tips that are worth checking out.
